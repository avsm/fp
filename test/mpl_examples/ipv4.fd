format ipv4 {
    version: bit[4] const(4);
    ihl: bit[4] min(5) value(offset(options) / 4);
    tos_precedence: bit[3] variant {
        |0 => Routine |1 -> Priority
        |2 -> Immediate |3 -> Flash
        |4 -> Flash_override |5 -> ECP
        |6 -> Internetwork_control |7 -> Network_control
    };
    tos_delay: bit variant {|0 => Normal |1 -> Low};
    tos_throughput: bit variant {|0 => Normal |1 -> Low};
    tos_reliability: bit variant {|0 => Normal |1 -> Low};
    tos_reserved: bit[2] const(0);
    length: int16 value(offset(data));
    id: int16;
    reserved: bit const(0);
    dont_fragment: bit default(0);
    can_fragment: bit default(0);
    frag_offset: bit[13] default(0);
    ttl: byte;
    protocol: byte variant {|1->ICMP |2->IGMP |6->TCP |17->UDP};
    checksum: int16 default(0);
    src: int32;
    dest: int32;
    options: byte[(ihl * 4) - offset(dest)];
    align(32);
    header_end: label;
    data: byte[length-(ihl*4)]
}
